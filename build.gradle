import static java.lang.System.getenv
buildscript {
    ext.kotlin_version = '1.1.60'

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://dl.bintray.com/kotlin/kotlin-eap'
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.15'
        classpath "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

task ('wrapper', type:Wrapper) {
    gradleVersion = '3.1'
}

project.group = 'com.meltwater.docker'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: "maven-publish"
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'groovy'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    compile "org.slf4j:slf4j-api:1.7.12"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "com.fasterxml.jackson.core:jackson-core:2.8.10"
    compile "com.fasterxml.jackson.core:jackson-databind:2.8.10"
    compile "org.apache.commons:commons-lang3:3.7"
    compile "commons-io:commons-io:2.6"
    compile "org.apache.maven:maven-artifact:3.0.5"

    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
    testCompile 'ch.qos.logback:logback-classic:1.2.3'

}

task version() {
    println project.version
}


dokka {
    includes = ['Module.md']

    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/meltwater/kotlin-compose"
        suffix = "#L"
    }
}


task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
    inputs.dir 'src/main/kotlin'
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}


task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}


publishing {
    publications {
        JavaArtifacts(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
            groupId project.group
            artifactId project.name
            version project.version
        }
    }
    repositories {
        maven {
            credentials {
                username getenv('MAVEN_USER')
                password getenv('MAVEN_PASSWORD')
            }
            url getenv('MAVEN_URL')
        }
    }
}

bintray {
    user = getenv('BINTRAY_USER')
    key = getenv('BINTRAY_KEY')
    publications = ['JavaArtifacts']
    publish = true
    pkg {
        repo = 'opensource'
        name = project.name
        userOrg = 'meltwater'
        licenses = ['MIT']
        vcsUrl = "https://github.com/meltwater/${project.name}"
        version {
            gpg {
                sign = true
            }
            released = new Date()
        }
    }
}